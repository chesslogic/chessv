
Game 'Duke of Rutlands Chess' : 'Generic Chess'
{
	NumFiles = 14;
	NumRanks = 10;
	Symmetry = MirrorSymmetry;
	Invented = "1747";
	InventedBy = "John, third Duke of Rutland";

	// Add a new game variable to allow selection of the castling rule
	Choice Castling;

	SetGameVariables
	{
		// Set the options for our castling option
		Castling = { "Castling", "No Castling" };
		Castling.DefaultValue = "Castling";

		Array = "rdnbbckqbbnndr/pppppppppppppp/14/14/14/14/14/14/PPPPPPPPPPPPPP/RDNBBCKQBBNNDR";
		PromotionTypes = "QCRDNB";
		EnPassant = true;
	}

	AddPieceTypes
	{
		AddPieceType( Queen, "Queen", "Q", 1250, 1350 );
		AddPieceType( Rook, "Rook", "R", 700, 750 );
		AddPieceType( Bishop, "Bishop", "B", 500, 500 );
		AddPieceType( Knight, "Knight", "N", 250, 250 );
		AddPieceType( Chancellor, "Concubine", "C", 100, 1050 );
		AddPieceType( 'Dragon King', "Crowned Rook", "D", 875, 900 );
	}

	AddRules
	{
		// Add pawn triple move when on the second rank (Rank == 1 because we start counting at zero)
		Pawn.AddMoveCapability( MoveCapability( <1, 0>, 3, 2, false, false ) ).Condition = { location: location.Rank == 1 };

		// Custom castling rule
		if( Castling == "Castling" )
		{
			AddCastlingRule();
			CastlingMove( 0, "g1", "k1", "n1", "j1", `N` );
			CastlingMove( 0, "g1", "c1", "a1", "d1", `A` );
			CastlingMove( 1, "g10", "k10", "n10", "j10", `n` );
			CastlingMove( 1, "g10", "c10", "a10", "d10", `a` );
		}
	}
}
